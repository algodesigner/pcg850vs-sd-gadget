100*INIT
110DT=7.13:DT$="0":BS$="10"
120*MENU
130KY$=""
140*MENU2
150LOCATE 0,0:PRINT " ***** n ¼Ý ´Ý»ÞÝ ***** "
160PRINT "   1:Æ­³Ø®¸   2:ÍÝ¶Ý    "
170PRINT "   3:Î½³      4:¹²»Ý    "
172PRINT "                        "
174LOCATE 0,5:PRINT "                        "
180IF DT=7.13 THEN DT=0:LOCATE 0,4:PRINT "(1,2,3,4)?              ":GOTO *KEY
190LOCATE 0,4:PRINT "["+BS$+"¼Ý]="
200IF KY$<>"" GOTO 270
210GOSUB *FROMDT
220LOCATE 7,4:PRINT "                ":LOCATE 24- LEN DT$,4:PRINT DT$
230*KEY
240IF INKEY$ <>"" THEN *KEY
250*KEY2
260GOSUB *GETKEY
270ON ASC KY$-&H30 GOTO *INDATA,*ROT,*REV,*CALC
280IF KY$="+" OR KY$="-" OR KY$="*" OR KY$="/" OR KY$="A" OR KY$="O" OR KY$="X" THEN *CALC2
290IF KY$="N" GOTO 810
300GOTO *KEY2
310*INDATA
320LOCATE 0,4:PRINT "["+BS$+"¼Ý]                  ":LOCATE 6,4:INPUT "=";DT$
330GOSUB *TTDT:IF CC=0 THEN *MENU
340GOSUB *ERROR:KY$="1":GOTO *MENU2
350*REV
360DT=-DT:GOTO *MENU
370*ROT
380IF BS$="10" THEN BS$="16":GOTO *MENU
390IF BS$="16" THEN BS$=" 2":GOTO *MENU
400BS$="10":GOTO *MENU
410*FROMDT
420LOCATE 7,4:PRINT "                 "
430GOSUB *ADJUST:CC=DT:DT$="":II= VAL BS$
440IF BS$="10" THEN DT$= STR$ CC:RETURN
450IF CC<0 THEN CC=CC+&H10000
460JJ=CC- INT (CC/II)*II:CC=(CC-JJ)/II
470IF JJ>9 THEN JJ=JJ+7
480DT$= CHR$ (48+JJ)+DT$
490IF CC>0 THEN 460
500IF BS$="16" THEN DT$= RIGHT$ ("0000"+DT$,4):RETURN
510DT$= RIGHT$ ("0000000000000000"+DT$,16):RETURN
520*TTDT
530CC=1:IF BS$<>"10" THEN 560
540IF LEFT$ (DT$,1)="+" THEN DT$= RIGHT$ (DT$, LEN DT$-1):GOTO 560
550IF LEFT$ (DT$,1)="-" THEN DT$= RIGHT$ (DT$, LEN DT$-1):CC=-1
560DT=0:FOR II= LEN DT$-1 TO 0 STEP -1:JJ= ASC MID$ (DT$, LEN DT$-II,1)-&H30
570IF 0<=JJ AND JJ< VAL BS$ AND JJ<10 THEN 600
580JJ=JJ-7:IF 10<=JJ AND JJ<16 AND JJ< VAL BS$ THEN 600
590CC=1:II=0:NEXT II:RETURN
600DT=DT* VAL BS$+JJ
610NEXT II
620IF ABS DT>=1E10 THEN CC=1:RETURN
630DT=DT*CC:CC=0:GOSUB *ADJUST:RETURN
640*CALC
650KY$=""
660*CALC2
670LOCATE 0,0:PRINT " ***** n ¼Ý ´Ý»ÞÝ ***** "
672PRINT "   1:Æ­³Ø®¸   2:ÍÝ¶Ý    "
674PRINT "   3:Î½³      4:¹²»Ý    "
676PRINT "                        "
680PRINT "["+BS$+"¼Ý]                  "
690LOCATE 24- LEN DT$,4:PRINT DT$
700IF KY$<>"" THEN 740
710LOCATE 0,5:PRINT "(+,-,*,/,A,O,N,X)?      "
720GOSUB *GETKEY
730IF KY$<>"+" AND KY$<>"-" AND KY$<>"*" AND KY$<>"/" AND KY$<>"A" AND KY$<>"O" AND KY$<>"N" AND KY$<>"X" THEN 720
740LOCATE 0,5:PRINT "                        ":LOCATE 0,5:TD=DT:TD$=DT$
750IF KY$="+" THEN INPUT "+";DT$
760IF KY$="-" THEN INPUT "-";DT$
770IF KY$="*" THEN INPUT "*";DT$
780IF KY$="/" THEN INPUT "/";DT$
790IF KY$="A" THEN INPUT "AND";DT$
800IF KY$="O" THEN INPUT "OR";DT$
810IF KY$="N" THEN DT= NOT DT:GOTO 930
820IF KY$="X" THEN INPUT "XOR";DT$
830GOSUB *TTDT:IF CC=0 THEN 850
840DT=TD:DT$=TD$:GOSUB *ERROR2:GOTO 660
850IF KY$="+" THEN DT=TD+DT
860IF KY$="-" THEN DT=TD-DT
870IF KY$="*" THEN DT=TD*DT
880IF (KY$="/") AND (DT=0) THEN 840
890IF KY$="/" THEN DT=TD/DT
900IF KY$="A" THEN DT=TD AND DT
910IF KY$="O" THEN DT=TD OR DT
920IF KY$="X" THEN DT=(TD AND ( NOT DT)) OR (( NOT TD) AND DT)
930GOSUB *ADJUST:GOTO *MENU
940*ADJUST
950DT= SGN DT* INT ABS DT
960IF DT>&HFFFF THEN DT=DT- INT (DT/&H10000)*&H10000
970IF DT>&H7FFF THEN DT=DT-&H10000
980IF DT<-&HFFFF THEN DT=DT+ INT (-DT/&H10000)*&H10000
990IF DT<-&H8000 THEN DT=DT+&H10000
1000RETURN
1010*ERROR
1020LOCATE 7,4:PRINT "ERROR            ":BEEP 2:RETURN
1030*ERROR2
1040LOCATE 0,5:PRINT "ERROR                   ":BEEP 2:RETURN
1050*GETKEY
1060II=2773*12
1070II=II-1:IF II<0 END
1080KY$= INKEY$ :IF KY$="" THEN 1070
1090RETURN
